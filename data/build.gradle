apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        develop {
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                showExceptions true
                exceptionFormat "full"
                showCauses true
                showStackTraces true
                showStandardStreams = false
            }
        }
        animationsDisabled true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.androidx.core_ktx
    implementation deps.androidx.preference
    implementation deps.apache.commons_io

    implementation deps.javax.inject

    implementation deps.kotlin.stdlib

    implementation deps.lifecycle.extensions

    implementation deps.retrofit.core
    implementation deps.retrofit.gson
    implementation deps.room.extensions
    implementation deps.room.runtime

    implementation deps.coroutines.core

    implementation deps.slf4j_timber

    implementation project(":domain")

    kapt deps.room.compiler

    androidTestImplementation deps.coroutines.test
    androidTestImplementation deps.espresso.core
    androidTestImplementation deps.instrumentationTest.junit_ext
    androidTestImplementation deps.instrumentationTest.test_rules
    androidTestImplementation deps.instrumentationTest.test_runner
    androidTestImplementation deps.truth

    androidTestImplementation project(":testshared")

    testImplementation deps.coroutines.test
    testImplementation deps.junit5.api
    testImplementation deps.mockito.core
    testImplementation deps.retrofit.gson
    testImplementation deps.robolectric
    testImplementation deps.truth

    testImplementation project(":testshared")

    testRuntimeOnly deps.junit5.engine

    ktlint deps.ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint
