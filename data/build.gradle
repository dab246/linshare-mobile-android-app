apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'de.mannodermaus.android-junit5'

android {
    compileSdkVersion build_versions.target_sdk
    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
    }
    buildTypes {
        develop {
        }
    }
    testOptions {
        unitTests.all {
            testLogging {
                events "passed", "skipped", "failed", "standardOut", "standardError"
                showExceptions true
                exceptionFormat "full"
                showCauses true
                showStackTraces true
                showStandardStreams = false
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

configurations {
    ktlint
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation deps.androidx.core_ktx

    implementation deps.javax.inject

    implementation deps.kotlin.stdlib

    implementation deps.retrofit.core

    implementation deps.coroutines.core

    implementation project(":domain")

    testImplementation deps.coroutines.test
    testImplementation deps.junit5.api
    testImplementation deps.mockito.core
    testImplementation deps.retrofit.gson
    testImplementation deps.truth

    testImplementation project(":testshared")

    testRuntimeOnly deps.junit5.engine

    ktlint deps.ktlint
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}
check.dependsOn ktlint
